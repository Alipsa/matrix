plugins {
  id 'groovy'
  id 'java-library'
  id 'signing'
  id 'maven-publish'
}

group 'se.alipsa.groovy'
version '1.0.1'
description = "Groovy library for importing structured text into a matrix"

// For some reason this is required to upgrade the wrapper
// Change the version and run 'gradle wrapper' to upgrade
task wrapper(type: Wrapper) {
  def gv = '8.10.2'
  gradleVersion = gv
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gv-bin.zip"
  validateDistributionUrl = true
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(JavaCompile) {
  options.deprecation = true
}

tasks.withType(GroovyCompile) { task ->
  task.options.deprecation = true
}

repositories {
  if (version.contains('SNAPSHOT')) {
    mavenLocal()
  }
  mavenCentral()
}

dependencies {
  def groovyVersion = '4.0.23'
  def groovy = "org.apache.groovy:groovy:$groovyVersion"
  if (version.endsWith('SNAPSHOT')) {
    compileOnly project(':matrix-core')
    testImplementation project(':matrix-core')
    testImplementation project(':matrix-datasets')
  } else {
    compileOnly('se.alipsa.groovy:matrix-core:2.0.0')
    testImplementation('se.alipsa.groovy:matrix-core:2.0.0')
    testImplementation('se.alipsa.groovy:matrix-datasets:1.0.4')
  }
  api 'org.apache.commons:commons-csv:1.12.0'
  compileOnly groovy
  testImplementation groovy

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'

}

test {
  testLogging.showStandardStreams = true
  useJUnitPlatform()
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
  archiveClassifier.set('javadoc')
  from groovydoc.destinationDir
}


task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Groovy Matrix CSV'
        description = "${project.description}"
        url = "https://github.com/Alipsa/matrix/matrix-csv"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/matrix/matrix-csv/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/matrix/matrix-csv/tree/master'
          connection = 'scm:git:https://github.com/Alipsa/matrix.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
        }
      }
    }
  }
}

signing {
  if (project.properties['signing.keyId'] != null) {
    project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
  } else {
    project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
  }
}