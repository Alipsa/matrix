import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
buildscript {
    dependencies {
        classpath 'se.alipsa.groovy:nexus-release-plugin:1.0.0-SNAPSHOT'
    }
}
plugins {
    id 'groovy'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

group 'se.alipsa.groovy'
version '1.0.1-SNAPSHOT'
description 'Groovy sql module for Matrix'

ext.nexusUrl = version.contains("SNAPSHOT")
    ? "https://oss.sonatype.org/content/repositories/snapshots/"
    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

// For some reason this is required to upgrade the wrapper
// Change the version and run 'gradle wrapper' to upgrade
task wrapper(type: Wrapper) {
    def gv = '8.8'
    gradleVersion = gv //version required
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gv-bin.zip"
    validateDistributionUrl = true
}

repositories {
    if (version.contains('SNAPSHOT')) {
        mavenLocal()
    }
    mavenCentral()
    maven {
        url = uri('https://repo.gradle.org/gradle/libs-releases')
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

tasks.withType(GroovyCompile) { task ->
    task.options.deprecation = true
}

dependencies {
    def groovyVersion = '4.0.22'
    def groovy = "org.apache.groovy:groovy:$groovyVersion"
    compileOnly groovy
    compileOnly project(':matrix-core')
    implementation 'se.alipsa.groovy:data-utils:1.0.5'
    implementation 'org.gradle:gradle-tooling-api:8.8'
    implementation 'commons-io:commons-io:2.16.1'
    implementation 'se.alipsa:maven-3.9.4-utils:1.0.3'
    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'

    testImplementation groovy
    testImplementation "org.apache.groovy:groovy-sql:$groovyVersion"
    testImplementation project(':matrix-core')
    testImplementation project(':matrix-datasets')
    //testImplementation 'com.h2database:h2:2.2.224' // dynamically fetched so not needed
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.3'
    testImplementation 'org.slf4j:slf4j-simple:2.0.13'
    testImplementation 'org.apache.logging.log4j:log4j-to-slf4j:2.23.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.23.1'
}

test {
    useJUnitPlatform()
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
            TestLogEvent.PASSED,
            TestLogEvent.SKIPPED,
            TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR,
                TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier.set 'javadoc'
    from groovydoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}


//Maven Central uploads
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            artifact(javadocJar)
            artifact(sourcesJar)
            pom {
                name = 'Matrix Core'
                description = "${project.description}"
                url = "https://github.com/Alipsa/matrix"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Alipsa/matrix/main/matrix-sql/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/Alipsa/matrix/tree/main/matrix-sql'
                    connection = 'scm:git:https://github.com/Alipsa/matrix.git'
                    developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
                }
            }
        }
    }
    if (project.ext.properties.sonatypeUsername) {
        repositories {
            maven {
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
                url = nexusUrl
            }
        }
    }
    //logger.lifecycle("Log in to https://oss.sonatype.org/index.htm to release the deployment")
}

signing {
    if (project.properties['signing.keyId'] != null) {
        project.logger.lifecycle("Signing artifacts...")
        sign publishing.publications.maven
        //sign configurations.archives
    }
}

if (project.ext.properties.sonatypeUsername) {

    apply plugin: 'nexus-release-plugin'
    //apply plugin: 'se.alipsa.groovy.NexusReleasePlugin'
    nexusReleasePlugin.nexusUrl = nexusUrl
    nexusReleasePlugin.userName = sonatypeUsername
    nexusReleasePlugin.password = sonatypePassword
}