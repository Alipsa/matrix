plugins {
    id 'groovy'
}

group = 'se.alipsa.groovy'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    if (version.contains('SNAPSHOT')) {
        mavenLocal()
    }
}

tasks.withType(JavaCompile) {
    options.deprecation = true
}

tasks.withType(GroovyCompile) { task ->
    task.options.deprecation = true
}

dependencies {
    implementation libs.groovy
    if (version.contains('SNAPSHOT') && rootProject.name != project.name) {
        // if we execute the build from the matrix root then we include project dependencies as SNAPSHOTS might not
        // have been published (or up to date).
        println "Adding dynamic project dependencies for matrix projects, root: ${rootProject.name}, project: ${project.name}"
        implementation project(':matrix-core')
        implementation project(':matrix-datasets')
        implementation project(':matrix-stats')
        implementation project(':matrix-charts')
    } else {
        implementation 'se.alipsa.matrix:matrix-core:1.2.4'
        implementation 'se.alipsa.matrix:matrix-datasets:1.0.3'
        implementation 'se.alipsa.matrix:matrix-stats:1.0.1'
        implementation 'se.alipsa.matrix:matrix-charts:1.0.0-SNAPSHOT'
    }
    testImplementation testLibs.junitJupiter
}

test {
    useJUnitPlatform()
}

def fatJarContainer = tasks.register('fatJar', Jar) {
    dependsOn(classes)
    archiveClassifier = 'fat'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
}


jar {
    dependsOn fatJarContainer
}