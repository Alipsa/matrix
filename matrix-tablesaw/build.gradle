import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
    id('groovy')
    id('java-library')
    id('signing')
    id('maven-publish')
    id('jacoco')
    //id("se.alipsa.nexus-release-plugin") version "1.0.1"
    id("se.alipsa.nexus-release-plugin") version '2.0.0'
    //id("com.github.ben-manes.versions").version("0.53.0")
}

group = 'se.alipsa.matrix'
version = '0.2.2'
description = 'Allows conversion between Matrix and Tablesaw'

repositories {
    if (version.contains('SNAPSHOT')) {
        mavenLocal()
    }
    mavenCentral()
}

JavaCompile javaCompile = compileJava {
    options.release = 21
    options.deprecation = true
    options.compilerArgs << "-Xlint:unchecked"
}

compileTestJava {
    options.deprecation = true
    options.compilerArgs << "-Xlint:unchecked"
}

compileGroovy {
    options.deprecation = true
    options.compilerArgs << "-Xlint:unchecked"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    def tablesawVersion = "0.44.4"
    compileOnly libs.groovy
    api("tech.tablesaw:tablesaw-core:${tablesawVersion}")
    compileOnly project(':matrix-core')
    compileOnly project(':matrix-stats')
    // for XlsxWriter, TODO: remove once contributed to tablesaw
    implementation 'org.apache.poi:poi-ooxml:5.5.1'
    // for OdsReader and Writer, TODO: remove once contributed to tablesaw
    implementation 'com.github.miachm.sods:SODS:1.7.0'
    // for XmlReader and writer, TODO: remove once contributed to tablesaw
    implementation 'org.dom4j:dom4j:2.2.0'

    testImplementation project(':matrix-core')
    testImplementation project(':matrix-stats')
    testImplementation "tech.tablesaw:tablesaw-json:${tablesawVersion}"
    testImplementation testLibs.junitJupiter
    testRuntimeOnly testLibs.junitPlatformLauncher
    testImplementation 'com.h2database:h2:2.4.240'
    testImplementation testLibs.slf4jSimple
    testImplementation libs.groovy
    testImplementation libs.groovySql
}

test {
    dependsOn cleanTest
    testLogging.showStandardStreams = true

    useJUnitPlatform()
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
            //TestLogEvent.PASSED,
            TestLogEvent.SKIPPED,
            TestLogEvent.STANDARD_OUT,
            TestLogEvent.STANDARD_ERROR
        exceptionFormat = TestExceptionFormat.FULL
        showExceptions = true
        showCauses = true
        showStackTraces = true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR,
                TestLogEvent.STANDARD_OUT
            exceptionFormat = TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

// JaCoCo configuration for code coverage
jacoco {
    toolVersion = "0.8.12"
}

def jacocoCoverageExclusions = [
    '**/*_closure*.class',
    '**/*Exception.class'
]

def jacocoTestExclusions = [
    '**/*Test.class',
    '**/*Test$*.class'
]

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoTestExclusions + jacocoCoverageExclusions)
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: jacocoTestExclusions + jacocoCoverageExclusions)
        }))
    }
    violationRules {
        rule {
            limit {
                // Current coverage: 54%
                // Target: 70% (aspirational goal for future releases)
                // Set to 50% to prevent regression while allowing incremental improvement
                minimum = 0.50
            }
        }
        rule {
            element = 'CLASS'
            // Exclude closure classes, exception classes, and low-coverage infrastructure classes
            excludes = [
                '*_closure*',
                '*Exception',
                // Builder classes have low coverage due to many untested builder combinations
                '*.Builder',
                // Aggregate function closures
                'tech.tablesaw.api.BigDecimalAggregateFunctions$*',
                // Infrastructure classes with low coverage (aspirational: add tests in future releases)
                'se.alipsa.matrix.tablesaw.gtable.GdataFrameJoiner',
                'tech.tablesaw.io.xlsx.XlsxWriter',
                'tech.tablesaw.io.xlsx.XlsxWriteOptions'
            ]
            limit {
                // Current: many classes below 30%
                // Set to 15% to establish baseline and prevent regression
                minimum = 0.15
            }
        }
    }
}

// Make check depend on coverage verification
check.dependsOn jacocoTestCoverageVerification

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'Matrix-Tablesaw integration'
                description = "${project.description}"
                url = "https://github.com/Alipsa/matrix/main/matrix-tablesaw"
                properties = [
                    'maven.compiler.release': "${javaCompile.options.release.get()}"
                ]
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Alipsa/matrix/main/matrix-tablesaw/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                    }
                }
                scm {
                    url = 'https://github.com/Alipsa/matrix/tree/main/matrix-tablesaw'
                    connection = 'scm:git:https://github.com/Alipsa/matrix.git'
                    developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
                }
            }
        }
    }
}

signing {
    if (project.properties['signing.keyId'] != null) {
        //project.logger.lifecycle("Signing artifacts...")
        sign publishing.publications.maven
    } else {
        project.logger.debug("signing.keyId is not defined, skipping signing of artifacts...")
    }
}

nexusReleasePlugin {
    userName = project.ext.properties.sonatypeUsername
    password = project.ext.properties.sonatypePassword
    mavenPublication = publishing.publications.maven
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
    gradleReleaseChannel = "current"
    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}