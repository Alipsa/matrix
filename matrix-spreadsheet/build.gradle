import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
  id('groovy')
  id('java-library')
  id('jacoco')
  id('signing')
  id('maven-publish')
  //id("se.alipsa.nexus-release-plugin") version "1.0.1"
  id("se.alipsa.nexus-release-plugin") version '2.0.0'
}

group = 'se.alipsa.matrix'
version = '2.3.0-SNAPSHOT'
description = 'Matrix spreadsheet import/export'

ext.nexusUrl = version.contains("SNAPSHOT")
    ? "https://oss.sonatype.org/content/repositories/snapshots/"
    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

repositories {
  if (version.contains('SNAPSHOT')) {
    mavenLocal()
  }
  mavenCentral()
}

JavaCompile javaCompile = compileJava {
  options.release = 21
  options.deprecation = true
  options.compilerArgs << "-Xlint:unchecked"
}

compileGroovy {
  options.deprecation = true
}

dependencies {
  compileOnly libs.groovy
  compileOnly libs.groovyAnt
  compileOnly libs.groovyTemplates
  compileOnly libs.groovyDoc
  compileOnly 'com.github.javaparser:javaparser-core:3.27.1'


  def fastExcelVersion = '0.19.0'
  compileOnly project(':matrix-core')
  testImplementation project(':matrix-core')
  implementation libs.log4jApi

  // https://github.com/dhatim/fastexcel
  implementation("org.dhatim:fastexcel:$fastExcelVersion")
  implementation("org.dhatim:fastexcel-reader:$fastExcelVersion")


  testImplementation testLibs.junitJupiterApi
  testRuntimeOnly testLibs.junitJupiterEngine
  testRuntimeOnly testLibs.junitPlatformLauncher
  testImplementation libs.groovy
  testImplementation testLibs.log4jToSlf4j
  testImplementation testLibs.slf4jSimple
}

test {
  jvmArgs '-Xmx2g'
  // Allow enabling from CLI:  -PrunSlowTests=true  (or env RUN_SLOW_TESTS=true)
  def runSlow = (project.findProperty('runSlowTests') ?: System.getenv('RUN_SLOW_TESTS'))?.toString()?.toBoolean() ?: false
  def odsProfile = (project.findProperty('odsProfile') ?: System.getenv('ODS_PROFILE'))?.toString()?.toBoolean() ?: false
  useJUnitPlatform {
    if (!runSlow) {
      excludeTags 'slow'
    }
  }
  systemProperty 'matrix.spreadsheet.ods.profile', odsProfile.toString()
  if (odsProfile) {
    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'info'
  }
  testLogging {
    // set options for log level LIFECYCLE
    events TestLogEvent.FAILED,
        //TestLogEvent.PASSED,
        TestLogEvent.SKIPPED,
        TestLogEvent.STANDARD_OUT,
        TestLogEvent.STANDARD_ERROR
    exceptionFormat = TestExceptionFormat.FULL
    showExceptions = true
    showCauses = true
    showStackTraces = true
    showStandardStreams = true

    // set options for log level DEBUG and INFO
    debug {
      events TestLogEvent.STARTED,
          TestLogEvent.FAILED,
          TestLogEvent.PASSED,
          TestLogEvent.SKIPPED,
          TestLogEvent.STANDARD_ERROR,
          TestLogEvent.STANDARD_OUT
      exceptionFormat = TestExceptionFormat.FULL
    }
    info.events = debug.events
    info.exceptionFormat = debug.exceptionFormat

    afterSuite { desc, result ->
      if (!desc.parent) { // will match the outermost suite
        def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        def startItem = '|  ', endItem = '  |'
        def repeatLength = startItem.length() + output.length() + endItem.length()
        println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
      }
    }
  }
}

// JaCoCo configuration for code coverage
jacoco {
  toolVersion = "0.8.12"
}

def jacocoCoverageExclusions = [
  '**/*_closure*.class',
  '**/*Exception.class'
]

def jacocoTestExclusions = [
  '**/*Test.class',
  '**/*Test$*.class'
]

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
    html.required = true
    csv.required = false
  }
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: jacocoTestExclusions + jacocoCoverageExclusions)
    }))
  }
}

jacocoTestCoverageVerification {
  dependsOn jacocoTestReport
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: jacocoTestExclusions + jacocoCoverageExclusions)
    }))
  }
  violationRules {
    rule {
      limit {
        counter = 'LINE'
        minimum = 0.70 // 70% overall coverage minimum
      }
    }
  }
}

check.dependsOn jacocoTestCoverageVerification

tasks.register('spreadsheetBenchmark', JavaExec) {
  group = 'benchmark'
  description = 'Run matrix-spreadsheet read/write benchmark'
  classpath = sourceSets.test.runtimeClasspath
  mainClass = 'spreadsheet.SpreadsheetBenchmark'
  def format = project.findProperty('format') ?: 'all'
  def rows = project.findProperty('rows') ?: '50000'
  def cols = project.findProperty('cols') ?: '12'
  def warmups = project.findProperty('warmups') ?: '1'
  def runs = project.findProperty('runs') ?: '3'
  def mode = project.findProperty('mode') ?: 'readwrite'
  args format, rows, cols, warmups, runs, mode
  if (project.hasProperty('odsProfile')) {
    systemProperty 'matrix.spreadsheet.ods.profile', project.findProperty('odsProfile')
    systemProperty 'org.slf4j.simpleLogger.defaultLogLevel', 'info'
  }
}

//docTitle = "${project.name} ${project.version}"
//windowTitle = "${project.name} ${project.version}"
//link 'https://docs.oracle.com/en/java/javase/21/docs/api/', 'java.'

tasks.register('antGroovydoc', DefaultTask) {
  group = 'documentation'
  description = 'Generate Groovydoc using Ant with custom config'

  def outputDir = layout.buildDirectory.dir("groovydocant")
  def groovySrcDirs = sourceSets.main.groovy.srcDirs
  def compileClasspath = sourceSets.main.compileClasspath
  def docTitle = "${project.name} ${project.version}"
  def windowTitle = "${project.name} ${project.version}"

  outputs.dir(outputDir)

  doLast {
    def outputDirFile = outputDir.get().asFile

    ant.taskdef(
        name: 'groovydoc',
        classname: 'org.codehaus.groovy.ant.Groovydoc',
        classpath: compileClasspath.asPath
    )

    ant.groovydoc(
        destdir: outputDirFile,
        sourcepath: groovySrcDirs.join(':'),
        packagenames: '*',
        javaVersion: 'JAVA_21',
        doctitle: docTitle,
        windowtitle: windowTitle
    ) {
      // Does not work, groovydoc does not support modules e,g,
      //  URL will become https://docs.oracle.com/en/java/javase/21/docs/api/java/net/URL.html
      //  instead of https://docs.oracle.com/en/java/javase/21/docs/api/java.base/java/net/URL.html
      //link packages: 'java.', href: 'https://docs.oracle.com/en/java/javase/21/docs/api/'
      link packages: "groovy.,org.codehaus.groovy.", href: "http://docs.groovy-lang.org/latest/html/api/"
    }
  }
}

tasks.named('build') {
  dependsOn tasks.named('antGroovydoc')
}

//Maven Central uploads

tasks.register('javadocJar', Jar) {
  dependsOn tasks.named('antGroovydoc')
  archiveClassifier.set('javadoc')
  from layout.buildDirectory.dir("groovydocant")
}

tasks.register('sourcesJar', Jar) {
  dependsOn tasks.named('classes')
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Matrix Spreadsheet import/export'
        description = "${project.description}"
        url = "https://github.com/Alipsa/matrix/matrix-spreadsheet"
        properties = [
            'maven.compiler.release': "${javaCompile.options.release.get()}"
        ]
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/matrix/matrix-spreadsheet/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/matrix/matrix-spreadsheet/tree/master'
          connection = 'scm:git:https://github.com/Alipsa/matrix.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
        }
      }
    }
  }
  if (project.ext.properties.sonatypeUsername) {
    repositories {
      maven {
        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
        url = nexusUrl
      }
    }
  }
}

signing {
  if (project.properties['signing.keyId'] != null) {
    //project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
  } else {
    project.logger.debug("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

nexusReleasePlugin {
  userName = project.ext.properties.sonatypeUsername
  password = project.ext.properties.sonatypePassword
  mavenPublication = publishing.publications.maven
}
