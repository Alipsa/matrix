import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.gradle.internal.os.OperatingSystem
plugins {
  id('groovy')
  id('java-library')
  //id 'org.openjfx.javafxplugin' version '0.0.13'
  id('maven-publish')
  id('signing')
  id("se.alipsa.nexus-release-plugin") version '2.0.1'
}

group = 'se.alipsa.matrix'
version = '0.5.0-SNAPSHOT'
description = "Groovy chart library for plotting with matrix ([][] data)"

ext.nexusUrl = version.contains("SNAPSHOT")
    ? "https://oss.sonatype.org/content/repositories/snapshots/"
    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

repositories {
  if (version.endsWith('SNAPSHOT')) {
    mavenLocal()
  }
  mavenCentral()
}

JavaCompile javaCompile = compileJava {
  options.release = 21
  options.deprecation = true
  options.compilerArgs << "-Xlint:unchecked"
}

compileGroovy {
  options.deprecation = true
}

// we should consider using monocle to allow this to work for back-end headless apps:
// https://wiki.openjdk.org/display/OpenJFX/Monocle
/*
this makes the javafx dependencies added to the pom
javafx {
  version = "19.0.2.1"
  modules = ['javafx.controls', 'javafx.swing']
}
 */

dependencies {
  OperatingSystem os = OperatingSystem.current()

  // check https://repo1.maven.org/maven2/org/openjfx/javafx/20/javafx-20.pom for the platform property
  def qualifier = 'unknown'
  if (os.isLinux()) {
    qualifier='linux'
  } else if (os.isWindows()) {
    qualifier = 'win'
  } else if (os.isMacOsX()) {
    qualifier = 'mac-aarch64'
  }
  def javaFxVersion = '23.0.2'
  // compile only dependencies are not included in the pom
  compileOnly libs.groovy
  compileOnly project(':matrix-core')
  compileOnly project(':matrix-stats')
  //compileOnly project(':matrix-groovy-ext')
  api("se.alipsa.groovy:gsvg:1.0.0")
  api("se.alipsa.groovy:gsvg-export:1.0.0")

  api 'se.alipsa:fxsvgimage:1.5.1'
  //api 'org.girod:fxsvgimage:1.4'
  api 'com.github.weisj:jsvg:2.0.0'

  compileOnly "org.openjfx:javafx-graphics:${javaFxVersion}:$qualifier"
  compileOnly "org.openjfx:javafx-base:${javaFxVersion}:$qualifier"
  compileOnly "org.openjfx:javafx-controls:${javaFxVersion}:$qualifier"
  compileOnly "org.openjfx:javafx-swing:${javaFxVersion}:$qualifier"

  testImplementation libs.groovy
  testImplementation libs.groovy.xml
  //testImplementation "org.testfx:openjfx-monocle:jdk-11+26"

  testImplementation project(':matrix-core')
  testImplementation project(':matrix-stats')
  testImplementation project(':matrix-datasets')
  testImplementation project(':matrix-ggplot')
  //testImplementation project(':matrix-groovy-ext')
  testImplementation "org.openjfx:javafx-graphics:${javaFxVersion}:$qualifier"
  testImplementation "org.openjfx:javafx-base:${javaFxVersion}:$qualifier"
  testImplementation "org.openjfx:javafx-controls:${javaFxVersion}:$qualifier"
  testImplementation "org.openjfx:javafx-swing:${javaFxVersion}:$qualifier"
  testImplementation libs.groovier.junit
  testImplementation libs.junit.jupiter
  testRuntimeOnly libs.junit.jupiter.engine
  testRuntimeOnly libs.junit.platform.launcher
  testImplementation libs.slf4j.simple
  testImplementation libs.groovy.sql
  //testImplementation 'org.testfx:testfx-core:4.0.16-alpha'
}

test {
  testLogging.showStandardStreams = true
  // run using gradle -Pheadless=true test
  jvmArgs "-Dheadless=${project.hasProperty('headless') ? project.headless : false}"
  def loggingConfig = file("${rootDir}/test-logging.properties")
  if (loggingConfig.exists()) {
    systemProperty 'java.util.logging.config.file', loggingConfig.absolutePath
  }
  maxParallelForks = Math.max(1, Runtime.getRuntime().availableProcessors().intdiv(2))
  useJUnitPlatform()
  testLogging {
    // set options for log level LIFECYCLE
    events TestLogEvent.FAILED,
        TestLogEvent.SKIPPED,
        TestLogEvent.STANDARD_OUT
    exceptionFormat = TestExceptionFormat.FULL
    showExceptions = true
    showCauses = true
    showStackTraces = true
    showStandardStreams = true

    // set options for log level DEBUG and INFO
    debug {
      events TestLogEvent.STARTED,
          TestLogEvent.FAILED,
          TestLogEvent.PASSED,
          TestLogEvent.SKIPPED,
          TestLogEvent.STANDARD_ERROR,
          TestLogEvent.STANDARD_OUT
      exceptionFormat = TestExceptionFormat.FULL
    }
    info.events = debug.events
    info.exceptionFormat = debug.exceptionFormat

    def totalDuration = 0
    afterSuite { desc, result ->
      def duration = result.endTime - result.startTime
      //println "Duration of $desc: $duration ms"
      totalDuration += duration
      if (!desc.parent) { // will match the outermost suite
        println "\nTotal test duration: ${totalDuration/1000} s"
        def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        def startItem = '|  ', endItem = '  |'
        def repeatLength = startItem.length() + output.length() + endItem.length()
        println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
      }
    }
  }
}

tasks.register('printCp') {
  description = "Utility task to print the classpath, excluding Groovy dependencies, to easily run examples"
  // Combine configurations into a single FileCollection
  def fullClasspath = configurations.testRuntimeClasspath +
      configurations.compileClasspath +
      tasks.jar.outputs.files

  inputs.files(fullClasspath)

  doLast {
    def filteredPath = fullClasspath.filter { file ->
      !file.path.contains("org/apache/groovy")
    }.asPath // .asPath handles the OS-specific separator ( : or ; )
    println filteredPath
  }
}

tasks.register('testFast', Test) {
  description = 'Runs only fast (non-render) tests. Use for quick dev-cycle feedback.'
  group = 'verification'
  testClassesDirs = sourceSets.test.output.classesDirs
  classpath = sourceSets.test.runtimeClasspath
  useJUnitPlatform {
    excludeTags 'slow'
  }
  jvmArgs "-Dheadless=${project.hasProperty('headless') ? project.headless : false}"
  maxParallelForks = Math.max(1, Runtime.getRuntime().availableProcessors().intdiv(2))
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
  archiveClassifier.set 'javadoc'
  from groovydoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier.set 'sources'
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Groovy Matrix Plotting'
        description = "${project.description}"
        url = "https://github.com/Alipsa/matrix/matrix-charts"
        properties = [
            'maven.compiler.release': "${javaCompile.options.release.get()}"
        ]
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/matrix/matrix-charts/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/matrix/matrix-charts/tree/master'
          connection = 'scm:git:https://github.com/Alipsa/matrix.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
        }
      }
    }
  }
  if (project.ext.properties.sonatypeUsername) {
    repositories {
      maven {
        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
        url = nexusUrl
      }
    }
  }
}


signing {
  if (project.properties['signing.keyId'] != null) {
    //project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
  } else {
    project.logger.debug("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

nexusReleasePlugin {
  userName = project.ext.properties.sonatypeUsername
  password = project.ext.properties.sonatypePassword
  mavenPublication = publishing.publications.maven
}