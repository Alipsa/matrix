import org.gradle.internal.os.OperatingSystem

plugins {
  id 'groovy'
  id 'java-library'
  //id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'maven-publish'
  id 'signing'
}

group 'se.alipsa.groovy'
version '1.0.0-SNAPSHOT'
description = "Groovy library for plotting with matrix ([][] data)"

repositories {
  if (version.contains('SNAPSHOT')) {
    mavenLocal()
  }
  mavenCentral()
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

// we should consider using monocle to allow this to work for back-end headless apps:
// https://wiki.openjdk.org/display/OpenJFX/Monocle
/*
this makes the javafx dependencies added to the pom
javafx {
  version = "19.0.2.1"
  modules = ['javafx.controls', 'javafx.swing']
}
 */

dependencies {
  OperatingSystem os = OperatingSystem.current()

  // check https://repo1.maven.org/maven2/org/openjfx/javafx/20/javafx-20.pom for the platform property
  def qualifier = 'unknown'
  if (os.isLinux()) {
    qualifier='linux'
  } else if (os.isWindows()) {
    qualifier = 'win'
  } else if (os.isMacOsX()) {
    qualifier = 'mac-aarch64'
  }
  def javaFxVersion = '22'
  def groovyVersion = '4.0.20'
  def groovy = "org.apache.groovy:groovy:$groovyVersion"
  // compile only dependencies are not included in the pom
  compileOnly groovy
  implementation project(':matrix-core')
  implementation project(':matrix-datasets')
  implementation project(':matrix-stats')

  // One of these is enough
  implementation "org.jfree:jfreechart:1.5.4"
  implementation 'org.knowm.xchart:xchart:3.8.7'

  compileOnly "org.openjfx:javafx-graphics:${javaFxVersion}:$qualifier"
  compileOnly "org.openjfx:javafx-base:${javaFxVersion}:$qualifier"
  compileOnly "org.openjfx:javafx-controls:${javaFxVersion}:$qualifier"
  compileOnly "org.openjfx:javafx-swing:${javaFxVersion}:$qualifier"

  testImplementation groovy
  //testImplementation "org.testfx:openjfx-monocle:jdk-11+26"

  testImplementation "org.openjfx:javafx-graphics:${javaFxVersion}:$qualifier"
  testImplementation "org.openjfx:javafx-base:${javaFxVersion}:$qualifier"
  testImplementation "org.openjfx:javafx-controls:${javaFxVersion}:$qualifier"
  testImplementation "org.openjfx:javafx-swing:${javaFxVersion}:$qualifier"
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
  testImplementation "org.apache.groovy:groovy-sql:$groovyVersion"
  //testImplementation 'org.testfx:testfx-core:4.0.16-alpha'
}

test {
  testLogging.showStandardStreams = true
  // run using gradle -Pheadless=true test
  jvmArgs "-Dheadless=${project.hasProperty('headless') ? project.headless : false}"
  useJUnitPlatform()
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
  archiveClassifier.set 'javadoc'
  from groovydoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier.set 'sources'
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Groovy Matrix Plotting'
        description = "${project.description}"
        url = "https://github.com/Alipsa/matrix/matrix-charts"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/matrix/matrix-charts/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/matrix/matrix-charts/tree/master'
          connection = 'scm:git:https://github.com/Alipsa/matrix.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
        }
      }
    }
  }
}


signing {
  if (project.properties['signing.keyId'] != null) {
    project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
  } else {
    project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
  }
}