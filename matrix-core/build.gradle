import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

plugins {
  id('groovy')
  id('java-library')
  id('signing')
  id('maven-publish')
  id('jacoco')
  id('codenarc')
  id("se.alipsa.nexus-release-plugin") version '2.0.0'
  //id("se.alipsa.nexus-release-plugin") version "1.0.1"
}

group = 'se.alipsa.matrix'
version = '3.6.0-SNAPSHOT'
description = "Groovy library for working with matrix ([][] data)"

JavaCompile javaCompile = compileJava {
  options.release = 21
  options.deprecation = true
  options.compilerArgs << "-Xlint:unchecked"
}

compileGroovy {
  options.deprecation = true
}

repositories {
  mavenCentral()
  if (version.endsWith('SNAPSHOT')) {
    mavenLocal()
  }
}

sourceSets {
  main {
    java { srcDirs = [] }    // no source dirs for the java compiler
    groovy { srcDirs = ["src/main/java", "src/main/groovy"] }  // compile   everything in src/ with groovy
  }
}

dependencies {
  api project(':matrix-groovy-ext')
  compileOnly libs.groovy
  testImplementation 'org.opentest4j:opentest4j:1.3.0'
  testImplementation libs.groovy
  testImplementation testLibs.junitJupiterApi
  testRuntimeOnly testLibs.junitJupiterEngine
  testRuntimeOnly testLibs.junitPlatformLauncher
  testImplementation 'org.apache.commons:commons-math3:3.6.1'
  testImplementation 'com.h2database:h2:2.4.240'
  testImplementation libs.groovySql
  testImplementation libs.groovyGinq
}

test {
  useJUnitPlatform()
  testLogging {
    // set options for log level LIFECYCLE
    events TestLogEvent.FAILED,
        //TestLogEvent.PASSED,
        TestLogEvent.SKIPPED,
        TestLogEvent.STANDARD_OUT,
        TestLogEvent.STANDARD_ERROR
    exceptionFormat = TestExceptionFormat.FULL
    showExceptions = true
    showCauses = true
    showStackTraces = true
    showStandardStreams = true

    // set options for log level DEBUG and INFO
    debug {
      events TestLogEvent.STARTED,
          TestLogEvent.FAILED,
          TestLogEvent.PASSED,
          TestLogEvent.SKIPPED,
          TestLogEvent.STANDARD_ERROR,
          TestLogEvent.STANDARD_OUT
      exceptionFormat = TestExceptionFormat.FULL
    }
    info.events = debug.events
    info.exceptionFormat = debug.exceptionFormat

    afterSuite { desc, result ->
      //def duration = result.endTime - result.startTime
      //println "Duration of $desc: $duration ms"
      if (!desc.parent) { // will match the outermost suite
        def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        def startItem = '|  ', endItem = '  |'
        def repeatLength = startItem.length() + output.length() + endItem.length()
        println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
      }
    }
  }
}

// JaCoCo configuration for code coverage
jacoco {
  toolVersion = "0.8.12"
}

def jacocoCoverageExclusions = [
  '**/*_closure*.class',
  '**/*Exception.class',
  '**/MatrixAssertions.class'
]

def jacocoTestExclusions = [
  '**/*Test.class',
  '**/*Test$*.class'
]

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
    html.required = true
    csv.required = false
  }
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: jacocoTestExclusions + jacocoCoverageExclusions)
    }))
  }
}

jacocoTestCoverageVerification {
  dependsOn jacocoTestReport
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude: jacocoTestExclusions + jacocoCoverageExclusions)
    }))
  }
  violationRules {
    rule {
      limit {
        minimum = 0.70 // 70% overall coverage minimum
      }
    }
    rule {
      element = 'CLASS'
      // Exclude closure classes, exception classes, and assertion utilities from per-class coverage
      excludes = [
        '*_closure*',
        '*Exception',
        '*MatrixAssertions' // Low coverage - needs dedicated assertion tests
      ]
      limit {
        minimum = 0.30 // 30% per class minimum (excluding closures)
      }
    }
  }
}

// CodeNarc configuration for static analysis
codenarc {
  toolVersion = '3.5.0'
  configFile = file('config/codenarc/ruleset.groovy')
  reportFormat = 'html'
  ignoreFailures = true // Set to false to fail build on violations; currently informational only
}

codenarcMain {
  source = 'src/main/groovy'
  // Exclude files with Groovy 5 switch expressions until CodeNarc fully supports them.
  // TODO: When upgrading CodeNarc to a version that supports Groovy 5 switch expressions,
  //       remove these exclusions so ValueConverter.groovy and MatrixBuilder.groovy are analyzed.
  exclude '**/ValueConverter.groovy'
  exclude '**/MatrixBuilder.groovy'
}

codenarcTest {
  source = 'src/test/groovy'
}

// Make check depend on quality checks
check.dependsOn jacocoTestCoverageVerification, codenarcMain

tasks.register('javadocJar', Jar) {
  dependsOn groovydoc
  archiveClassifier.set 'javadoc'
  from groovydoc.destinationDir
}


tasks.register('sourcesJar', Jar) {
  dependsOn classes
  archiveClassifier.set 'sources'
  from sourceSets.main.allSource
}


//Maven Central uploads
publishing {
  publications {
    maven(MavenPublication) {
      from components.java

      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Matrix Core'
        description = "${project.description}"
        url = "https://github.com/Alipsa/matrix"
        properties = [
            'maven.compiler.release': "${javaCompile.options.release.get()}"
        ]
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/matrix/main/matrix-core/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/matrix/tree/main/matrix-core'
          connection = 'scm:git:https://github.com/Alipsa/matrix.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
        }
      }
    }
  }
  if (project.ext.properties.sonatypeUsername) {
    repositories {
      maven {
        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
        //url = releaseNexusUrl
      }
    }
  }
  //logger.lifecycle("Log in to https://oss.sonatype.org/index.htm to release the deployment")
}

signing {
  if (project.properties['signing.keyId'] != null) {
    //project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
    //sign configurations.archives
  } else {
    project.logger.debug("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

nexusReleasePlugin {
  userName = project.ext.properties.sonatypeUsername
  password = project.ext.properties.sonatypePassword
  mavenPublication = publishing.publications.maven
}
