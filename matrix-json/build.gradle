buildscript {
  dependencies {
    classpath 'se.alipsa.groovy:nexus-release-plugin:1.0.0-SNAPSHOT'
  }
}
plugins {
  id 'groovy'
  id 'java-library'
  id 'signing'
  id 'maven-publish'
}

group 'se.alipsa.matrix'
version '1.1.0-SNAPSHOT'
description = "Groovy library for importing and exporting json to and from a Matrix or Grid"

ext.nexusUrl = version.toString().endsWith("SNAPSHOT")
    ? "https://oss.sonatype.org/content/repositories/snapshots/"
    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

task wrapper(type: Wrapper) {
  def gv = '8.10.2'
  gradleVersion = gv
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gv-bin.zip"
  validateDistributionUrl = true
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(JavaCompile) {
  options.deprecation = true
}

tasks.withType(GroovyCompile) { task ->
  task.options.deprecation = true
}

repositories {
  if (version.toString().endsWith('SNAPSHOT')) {
    mavenLocal()
  }
  mavenCentral()
}

dependencies {

  if (version.endsWith('SNAPSHOT')) {
    compileOnly project(':matrix-core')
    testImplementation project(':matrix-core')
    testImplementation project(':matrix-datasets')
  } else {
    compileOnly('se.alipsa.matrix:matrix-core:2.1.1')
    testImplementation('se.alipsa.matrix:matrix-core:2.1.1')
    testImplementation('se.alipsa.matrix:matrix-datasets:1.0.4')
  }

  compileOnly libs.groovy
  compileOnly libs.groovyJson

  testImplementation libs.groovy
  testImplementation libs.groovyJson
  testImplementation testLibs.junitJupiterApi
  testRuntimeOnly testLibs.junitJupiterEngine
  testImplementation('com.fasterxml.jackson.core:jackson-core:2.18.0')
  testImplementation('com.fasterxml.jackson.core:jackson-databind:2.18.0')
}

test {
  testLogging.showStandardStreams = true
  useJUnitPlatform()
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
  archiveClassifier.set('javadoc')
  from groovydoc.destinationDir
}


task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Groovy Matrix Json'
        description = "${project.description}"
        url = "https://github.com/Alipsa/matrix/matrix-json"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/matrix/matrix-json/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/matrix/matrix-json/tree/master'
          connection = 'scm:git:https://github.com/Alipsa/matrix.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
        }
      }
    }
  }
  if (project.ext.properties.sonatypeUsername) {
    repositories {
      maven {
        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
        url = nexusUrl
      }
    }
  }
}

signing {
  if (project.properties['signing.keyId'] != null) {
    project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
  } else {
    project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

if (project.ext.properties.sonatypeUsername) {

  apply plugin: 'nexus-release-plugin'
  //apply plugin: 'se.alipsa.groovy.NexusReleasePlugin'
  nexusReleasePlugin.nexusUrl = nexusUrl
  nexusReleasePlugin.userName = sonatypeUsername
  nexusReleasePlugin.password = sonatypePassword
}