plugins {
  id 'groovy'
  id 'java-library'
  id 'signing'
  id 'maven-publish'
  id("se.alipsa.nexus-release-plugin") version '2.0.0'
}

group = 'se.alipsa.matrix'
version = '0.5.0-SNAPSHOT'
description = "Groovy library for importing and exporting to and from Google BigQuery to a Matrix"

ext.releaseNexusUrl = version.toString().endsWith("SNAPSHOT")
    ? "https://oss.sonatype.org/content/repositories/snapshots/"
    : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

JavaCompile javaCompile = compileJava {
  options.release = 21
  options.deprecation = true
  options.compilerArgs << "-Xlint:unchecked"
}

compileGroovy {
  options.deprecation = true
}

repositories {
  if (version.toString().endsWith('SNAPSHOT')) {
    mavenLocal()
  }
  mavenCentral()
}

dependencies {
  compileOnly project(':matrix-core')
  testImplementation project(':matrix-core')
  testImplementation project(':matrix-datasets')
  compileOnly libs.groovy

  api('com.google.cloud:google-cloud-bigquery:2.56.0')
  api 'com.google.cloud:google-cloud-resourcemanager:1.82.0'
  implementation platform('com.google.auth:google-auth-library-bom:1.41.0')
  implementation 'com.google.auth:google-auth-library-oauth2-http'
  implementation('com.google.cloud:google-cloud-bigquerystorage:3.18.0')
  implementation 'me.tongfei:progressbar:0.10.1'

  // https://mvnrepository.com/artifact/org.testcontainers/gcloud
  testImplementation 'org.testcontainers:gcloud:1.21.3'
  testImplementation 'org.testcontainers:junit-jupiter:1.21.3'
  testImplementation libs.groovy
  testImplementation testLibs.junitJupiterApi
  testRuntimeOnly testLibs.junitJupiterEngine
  testRuntimeOnly testLibs.junitPlatformLauncher
  testRuntimeOnly testLibs.log4jToSlf4j
}

test {
  testLogging.showStandardStreams = true
  useJUnitPlatform() {
    excludeTags 'external', 'flaky'
  }

  // Allow enabling from CLI:  -PrunExternalTests=true  (or env RUN_EXTERNAL_TESTS=true)
  def runExternal = (project.findProperty('runExternalTests') ?: System.getenv('RUN_EXTERNAL_TESTS'))?.toString()?.toBoolean() ?: false
  if (runExternal) {
    useJUnitPlatform {
      excludeTags()          // clear the default exclusion
    }
  }
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
  archiveClassifier.set('javadoc')
  from groovydoc.destinationDir
}


task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Matrix Json'
        description = "${project.description}"
        url = "https://github.com/Alipsa/matrix/matrix-bigquery"
        properties = [
            'maven.compiler.release': "${javaCompile.options.release.get()}"
        ]
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/matrix/matrix-bigquery/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/matrix/matrix-bigquery/tree/master'
          connection = 'scm:git:https://github.com/Alipsa/matrix.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/matrix.git'
        }
      }
    }
  }
  if (project.ext.properties.sonatypeUsername) {
    repositories {
      maven {
        credentials {
          username = sonatypeUsername
          password = sonatypePassword
        }
        url = releaseNexusUrl
      }
    }
  }
}

signing {
  if (project.properties['signing.keyId'] != null) {
    //project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
  } else {
    project.logger.debug("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

nexusReleasePlugin {
  userName = project.ext.properties.sonatypeUsername
  password = project.ext.properties.sonatypePassword
  mavenPublication = publishing.publications.maven
}